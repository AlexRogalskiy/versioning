/**
 * Versioning applied to itself
 */
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.nemerosa:versioning:0.3.3'
    }
}

/**
 * Plug-in development
 */

plugins {
   id 'nu.studer.plugindev' version '1.0.3'
   id 'com.jfrog.bintray' version '1.0'
}

apply plugin: 'net.nemerosa.versioning'
apply plugin: 'groovy'

/**
 * Meta information
 */

group = 'net.nemerosa'
version = '0.3.3'

/**
 * Dependencies
 */

dependencies {
    localGroovy()
}

/**
 * Plug-in definition
 */

plugindev {
    pluginImplementationClass 'net.nemerosa.versioning.VersioningPlugin'
    pluginDescription 'Gradle plug-in that computes version information from the SCM'
    pluginLicenses 'MIT'
    pluginTags 'gradle', 'plugin', 'scm', 'git', 'svn', 'version'
    authorId 'dcoraboeuf'
    authorName 'Damien Coraboeuf'
    authorEmail 'damien.coraboeuf@gmail.com'
    projectUrl 'https://github.com/nemerosa/versioning'
    projectInceptionYear '2015'
    done()
}

/**
 * Plug-in publication
 */

bintray {
    user = "${BINTRAY_USER}"
    key = "${BINTRAY_API_KEY}"
    pkg {
        repo = 'nemerosa'
        version {
            attributes = [
                    'gradle-plugin': 'net.nemerosa.versioning:net.nemerosa:versioning'
            ]
        }
    }
}

/**
 * Wrapper definition
 */

task wrapper(type: Wrapper) {
   gradleVersion = '2.2.1'
}
